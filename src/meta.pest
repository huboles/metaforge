WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT    = _{ "-{" ~ (!"}" ~ ANY)* ~ "}" }

sigil = _{ ("$" | "@" | "&" | "#" | "-") ~ "{" }

raw_char = _{ !(sigil) ~ ANY }
char_seq =  ${ raw_char+ }

string = { escaped_string | raw_string }
escaped_string = @{ "\"" ~ char+ ~ "\"" }
raw_string = @{ "'" ~ (!("'") ~ ANY)+ ~ "'" }
char   =  _{
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

array = _{
    "[" ~ "]"
  | "[" ~ " "* ~ string ~ (" "* ~ "," ~ " "* ~ string)* ~ " "* ~ "]"
}

key_chars = @{ (ASCII_ALPHANUMERIC | "_" | ".")* }
key       = @{ key_chars }

value  = ${ string | array | "BLANK" | "DEFAULT" }
assign =  { "*"? ~ key ~ "=" ~ value }

def_block  = _{ sigil ~ assign* ~ "}" }
var_def    =  { &("$") ~ def_block }
arr_def    =  { &("@") ~ def_block }
pat_def    =  { &("&") ~ def_block }
definition = _{ var_def | arr_def | pat_def }

header_assign =  { "!"? ~ key ~ "=" ~ value }
header_block  = _{ sigil ~ header_assign* ~ "}" }
header    =  { &("#") ~ header_block }

substitution = _{ sigil ~ key ~ "}" }
var_sub      =  { &("$") ~ substitution }
arr_sub      =  { &("@") ~ substitution }
pat_sub      =  { &("&") ~ substitution }
identifier   = _{ var_sub | pat_sub | arr_sub }

source = { (identifier | char_seq)* }

file = {
    SOI ~ header? ~ definition* ~ source? ~ EOI
}
